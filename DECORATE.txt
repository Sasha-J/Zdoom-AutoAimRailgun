ACTOR AutoAimRailgun : Shotgun
{
	Obituary "ZAP!"
	Tag "AutoAimRailgun"
	Weapon.Selectionorder 100
	Weapon.AmmoUse 5
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.PickupMessage "You picked up a railgun thing"
	Weapon.SlotNumber 6
	States
	{
	Fire:
		TNT1 A 0 A_GunFlash
		// Fire a 0 damage bullet with a custom puff. The custom puff's X, Y, and Z
		// values are used to calculate the angle the RailAttack needs to be. See actor AutoAimPuff.
		SHTG A 1 A_FireBullets(0,0,-1,0,"AutoAimPuff")
		// Fire the RailAttack, but executes a script that sets the pitch to the result of a script.
		// See /RailACS.acs
		SHTG A 4 A_RailAttack(40,0,1,"none","A0 A0 A0",RGF_EXPLICITANGLE, 0,"BulletPuff", 0,
			callAcs("getRelativePitch") / 65536,0,70,1.5,0.2)
		Goto Ready
	Flash:
		SHTF A 2 Bright A_Light1
		SHTF B 1 Bright A_Light2
		Goto LightDone
	}
}

ACTOR AutoAimPuff
{
    Mass 0
    Radius 1
    Height 1
    +NOBLOCKMAP
    +NOGRAVITY
	+PUFFGETSOWNER
	//The following flags make sure the puff doesn't change to something it's not supposed to be
	+BLOODLESSIMPACT
    +ALWAYSPUFF
    +PUFFONACTORS
    +DONTSPLASH
    States
    {
    Spawn:
		// Change the TID, to be accessed by the script. See /RailAcs.acs
		// -1 duration is so the puff stays until it's not needed anymore.
		TNT1 A -1 NODELAY Thing_ChangeTID(0,CallACS("makePuffTid"))
		Stop
    }
}